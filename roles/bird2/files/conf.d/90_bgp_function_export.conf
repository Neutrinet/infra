function bgp_export(int ip_version; int AS; string export_type; string locality)
{

    if ! (export_type = "core" || export_type="upstream" || export_type="downstream" || export_type="default_gateway") then {
        print "bgp_export: parametre export_type invalide";
        return false;
    }

    if ! (locality = "louisedc" || locality = "bnix" || locality = "belgiumix" || locality = "local") then {
        print "bgp_export: parametre locality invalide";
        return false;
    }

    if ! (ip_version = 6 || ip_version = 4) then {
        print "bgp_export: parametre ip_version invalide";
        return false;
    }

    if (export_type = "default_gateway") then {
        if ip_version = 6 && (net = ::/0) then {
            return true;
        }
        if ip_version = 4 && (net = 0.0.0.0/0) then {
            return true;
        }

        return false;
    }

    if export_type =! "core" && source = RTS_DEVICE then return false;

    if ip_version = 4 then krt_prefsrc = my_krt_prefsrc_v4;
    if ip_version = 6 then krt_prefsrc = my_krt_prefsrc_v6;

    if export_type = "upstream" then {
        if ip_version = 4 && ! (net ~ my_prefix_v4 || bgp_path ~ my_asn_downstream) then return false;
        if ip_version = 6 && ! (net ~ my_prefix_v6 || bgp_path ~ my_asn_downstream) then return false;

        if ddos_telia_protection then {
            if AS = 6696 then {
                bgp_community.add((6696, 777));
            }
            else return false;
        }
    }

    if export_type = "core" then {
        if ip_version = 6 && (net ~ filter_my_prefix_v6) && (net.len > 36) then return true;
        if ip_version = 4 && (net ~ filter_my_prefix_v4) && (net.len > 24) then return true;
    }

    if ip_version = 6 then {
        if reject_v6_bogon_prefixes("export", locality)    then return false;
        if match_rtbh_v6("export", locality)               then return true;
        if reject_v6_small_prefixes("export", locality)    then return false;
        if reject_v6_default_route("export", locality)     then return false;
        if reject_v6_invalid_rpki("export", locality)      then return false;
    }

    if ip_version = 4 then {
        if reject_v4_bogon_prefixes("export", locality)    then return false;
        if match_rtbh_v4("export", locality)               then return true;
        if reject_v4_small_prefixes("export", locality)    then return false;
        if reject_v4_default_route("export", locality)     then return false;
        if reject_v4_invalid_rpki("export", locality)      then return false;
    }

    if export_type = "core" || export_type = "upstream" || export_type = "downstream" then return true;
    if export_type = "default_gateway" then return false;

}
